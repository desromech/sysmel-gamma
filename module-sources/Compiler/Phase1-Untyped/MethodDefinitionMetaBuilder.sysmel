## Type definition builder.
__BootstrapCompiler__
    enterTopLevelNamespace;
    enterNamespaceNamed: #MetaProgramming;
    addBindingNamed: #MethodDefinitionMetaBuilder with: (__TypeBuilder__ newGCClassWithSuperclass: MetaProgramming MetaBuilder slots: #{
        hasSeenMethodPrototype: Boolean.
        selectorExpression: Reflection AST Node optional.
        arguments: Reflection AST Node sharedArraySlice.
        resultTypeExpression: Reflection AST Node optional.
        body: Reflection AST Node optional.
    }).

MetaProgramming MethodDefinitionMetaBuilder
    withSelector: #analyzeMessageSendNode:with: addMethod: {:(MetaProgramming MethodDefinitionMetaBuilder)self :(Reflection AST MessageSendNode)node :(Reflection Semantic ASTAnalyzer)analyzer :: Reflection AST Node |
        if: self hasSeenMethodPrototype not then: {
            self
                hasSeenMethodPrototype: true;
                selectorExpression: node selector;
                arguments: (node arguments collect: {:(Reflection AST Node)argumentNode :: Reflection AST Node |
                    argumentNode parseAsArgumentDefinitionNode
                }).
            node receiver _
        } else: {
            self analyzeMetaBuilderDirectMessageNode: node with: analyzer
        }
    };
    withSelector: #=> addMethod: {:(MetaProgramming MethodDefinitionMetaBuilder)self :(Reflection AST Node)resultTypeExpression :: Void |
        <metaBuilder>
        self resultTypeExpression: resultTypeExpression.
        void
    };
    withSelector: #":=" addMethod: {:(MetaProgramming MethodDefinitionMetaBuilder)self :(Reflection AST Node)body :: Void |
        <metaBuilder>
        self body: body.
        self finish.
    };
    withSelector: #concretizeMetaBuilder addMethod: {:(MetaProgramming MethodDefinitionMetaBuilder)self :: Reflection AST Node |
        (Reflection AST MethodDefinitionNode basicNewWithNamedSlots: #{
            sourcePosition: self metaContext sourcePosition.
            selectorExpression: self selectorExpression.
            arguments: self arguments.
            resultTypeExpression: self resultTypeExpression.
            body: self body.
            visibility: self programEntityAttributes visibility.
        })
    }.

MetaProgramming FlagMetaBuilder
    withSelector: #method addMethod: {:(MetaProgramming FlagMetaBuilder)self :: Void |
        <metaBuilder>
        self delegateToMetaBuilderClass: MetaProgramming MethodDefinitionMetaBuilder.
        void
    }.
