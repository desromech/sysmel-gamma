__BootstrapCompiler__
    enterTopLevelNamespace;
    enterNamespaceNamed: #Reflection;
    enterNamespaceNamed: #AST;
    addBindingNamed: #NamespaceDefinitionNode with: (__TypeBuilder__ newGCClassWithSuperclass: Reflection AST ProgramEntityDefinitionNode publicSlots: #{
        nameExpression: Reflection AST Node optional.
        bodyExpression: Reflection AST Node optional.
        bodyLexicalScope: Reflection Semantic LexicalScope optional
    }).


Reflection ProgramEntity
    withSelector: #getOrCreateChildNamespaceNamed: addMethod: {:(Reflection ProgramEntity)self :(Object)name :: Reflection Namespace |
        __BootstrapCompiler__ subclassResponsibility
    }.

Reflection Namespace
    withSelector: #isDefinitionOwner addMethod: {:(Reflection Namespace)self :: Boolean |
        true
    };
    withSelector: #canHaveNamespaces addMethod: {:(Reflection Namespace)self :: Boolean |
        true
    };
    withSelector: #getOrCreateChildNamespaceNamed: addMethod: {:(Reflection ProgramEntity)self :(Object)name :: Reflection Namespace |
        <primitive: #"namespace.getOrCreateChildNamespaceNamed">
        __BootstrapCompiler__ primitiveFailed
    };
    withSelector: #lookupPublicSymbol: addMethod: {:(Reflection Namespace)self :(Object)symbol :: Reflection SymbolBinding optional |
        self symbolTable lookupSymbol: symbol
    };
    withSelector: #lookupScopeSymbol: addMethod: {:(Reflection Namespace)self :(Object)symbol :: Reflection SymbolBinding optional |
        self symbolTable lookupSymbol: symbol
    }.

Reflection AST NamespaceDefinitionNode
    withSelector: #analyzeNodeWith: addMethod: {:(Reflection AST NamespaceDefinitionNode)self :(Reflection Semantic ASTAnalyzer)analyzer :: Reflection AST Node |
        let: #ownerProgramEntity with: (analyzer environment lexicalScope lookupCurrentProgramEntityDefinitionOwner).
        let: #hasValidOwner with: (if: ownerProgramEntity isNotNil then: ownerProgramEntity _ canHaveNamespaces else: false).

        if: hasValidOwner then: {
            let: #name with: (if: self nameExpression isNotNil
                then: (self nameExpression _ analyzeAndEvaluateNodeWith: analyzer)
                else: (AnonymousSymbol basicNewWithNamedSlots: #{
                    sourcePosition: self sourcePosition
                })
            ).
            let: #sanitizedName with: (name asSanitizedNameSymbolFor: self sourcePosition).
            let: #childNamespace with: (ownerProgramEntity _ getOrCreateChildNamespaceNamed: sanitizedName).

            let: #analyzedNode with: self shallowCopy.

            analyzedNode
                nameExpression: nil;
                analyzedProgramEntity: childNamespace;
                analyzedType: childNamespace __type__.

            if: analyzedNode bodyExpression isNotNil then: {
                let: #namespaceScope with: (Reflection Semantic NamespaceLookupScope basicNewWithNamedSlots: #{
                    parent: analyzer environment lexicalScope.
                    programEntity: childNamespace
                }).
                let: #bodyLexicalScope with: namespaceScope makeChildLexicalScope.
                let: #bodyEnvironment with: (analyzer environment copyWithLexicalScope: bodyLexicalScope).
                
                analyzedNode
                    bodyExpression: (analyzer analyzeNodeIfNeeded: analyzedNode bodyExpression _ withEnvironment: bodyEnvironment expectedType: Void);
                    bodyLexicalScope: bodyLexicalScope.
            } else: { 
                void
            }.

            __BootstrapCompiler__ print: analyzedNode.
            analyzedNode
        } else: {
            analyzer makeSemanticAnalysisError: "A namespace cannot be defined in this location." at: self sourcePosition.
        }
    };
    withSelector: #evaluateNodeWith: addMethod: {:(Reflection AST NamespaceDefinitionNode)self :(Reflection Semantic EvaluationEnvironment)evaluationEnvironment :: AnyValue |
        if: self bodyExpression isNotNil then: {
            let: #namespaceScope with: (Reflection Semantic NamespaceLookupScope basicNewWithNamedSlots: #{
                parent: evaluationEnvironment lexicalScope.
                programEntity: self analyzedProgramEntity _
            }).
            let: #bodyLexicalScope with: namespaceScope makeChildLexicalScope.
            let: #bodyEnvironment with: (evaluationEnvironment copyWithLexicalScope: bodyLexicalScope).

            self bodyExpression _ evaluateNodeWith: bodyEnvironment.
        } else: {void}.

        self analyzedProgramEntity _
    }.