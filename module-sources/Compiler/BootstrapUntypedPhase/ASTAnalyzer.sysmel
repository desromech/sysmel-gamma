Std Reflection Semantic ASTAnalyzer __type__
    withSelector: #forEnvironment: addMethod: {:(Std Reflection Semantic ASTAnalyzer __type__)self :(Std Reflection Semantic EvaluationEnvironment)environment :: self |
        self#{
            environment: environment.
            typeInferenceEnvironment: Std Reflection Semantic AutoTypeInferenceEnvironment basicNew.
        }
    };
    withSelector: #forEnvironment:withExpectedType: addMethod: {:(Std Reflection Semantic ASTAnalyzer __type__)self :(Std Reflection Semantic EvaluationEnvironment)environment :(Type)expectedType :: self |
        self#{
            environment: environment.
            typeInferenceEnvironment: (Std Reflection Semantic ExpectedTypeInferenceEnvironment#{
                expectedType: expectedType
            }).
        }
    };
    withSelector: #forScriptEvaluationScope: addMethod: {:(Std Reflection Semantic ASTAnalyzer __type__)self :(Std Reflection Semantic ScriptEvaluationScope)scope :: self |
        self forEnvironment: (Std Reflection Semantic EvaluationEnvironment forScriptEvaluationScope: scope)
    };
    withSelector: #forDefaultEnvironment addMethod: {:(Std Reflection Semantic ASTAnalyzer __type__)self :: self |
        self forEnvironment: Std Reflection Semantic EvaluationEnvironment default
    }.

Std Reflection Semantic ASTAnalyzer
    withSelector: #makeSemanticAnalysisError:at: addMethod: {:(Std Reflection Semantic ASTAnalyzer)self :(String)errorMessage :(Std Reflection SourcePosition)sourcePosition :: Std Reflection AST SemanticErrorNode |
        __BootstrapCompiler__ printLine: errorMessage.
        __BootstrapCompiler__ printLine: ("{0}: {1}" format: (sourcePosition printString, errorMessage)).
        Std Reflection AST SemanticErrorNode#{
            hasCompilationError: true.
            analyzedType: CompilationError.
            sourcePosition: sourcePosition.
            message: errorMessage.
        }.
    };
    withSelector: #makeSemanticAnalysisError:atNode: addMethod: {:(Std Reflection Semantic ASTAnalyzer)self :(String)errorMessage :(Std Reflection AST Node)node :: Std Reflection AST SemanticErrorNode |
        __BootstrapCompiler__ printLine: ("{0}: {1}" format: (sourcePosition printString, errorMessage)).
        Std Reflection AST SemanticErrorNode#{
            analyzedType: (if: node analyzedType isNil then: CompilationError else: node analyzedType _).
            hasCompilationError: true.
            sourcePosition: node sourcePosition.
            message: errorMessage.
            innerNode: node
        }.
    };

    withSelector: #computeBranchResultTypeOf:with: addMethod: {:(Std Reflection Semantic ASTAnalyzer)self :(Type)leftType :(Type)rightType :: Type |
        if: leftType == rightType then: {
            leftType
        } else: {
            Void
        }
    };
    withSelector: #addImplicitCoercionTo:node: addMethod: {:(Std Reflection Semantic ASTAnalyzer)self :(Type)targetType :(Std Reflection AST Node)node :: Std Reflection AST Node |
        node analyzedType _ addImplicitCoercionToNode: node targetType: targetType with: self
    };
    withSelector: #withEnvironment:do: addMethod: {:(Std Reflection Semantic ASTAnalyzer)self :(Std Reflection Semantic EvaluationEnvironment)environment :(Void => Void)aBlock :: Void |
        let: #oldEnvironment with: self environment.
        self environment: environment.

        aBlock().
        self environment: oldEnvironment.

        void
    };
    withSelector: #analyzeNodeIfNeeded:withEnvironment: addMethod: {:(Std Reflection Semantic ASTAnalyzer)self :(Std Reflection AST Node)node :(Std Reflection Semantic EvaluationEnvironment)environment :: Std Reflection AST Node |
        let: #oldEnvironment with: self environment.
        self environment: environment.

        let: #analyzedNode with: (self analyzeNodeIfNeededWithCurrentExpectedType: node).
        self environment: oldEnvironment.

        analyzedNode
    };
    withSelector: #analyzeNodeIfNeeded:withEnvironment:expectedType: addMethod: {:(Std Reflection Semantic ASTAnalyzer)self :(Std Reflection AST Node)node :(Std Reflection Semantic EvaluationEnvironment)environment :(Type)expectedType :: Std Reflection AST Node |
        let: #oldEnvironment with: self environment.
        self environment: environment.

        let: #analyzedNode with: (self analyzeNodeIfNeeded: node withExpectedType: expectedType).
        self environment: oldEnvironment.

        analyzedNode
    };
    withSelector: #analyzeNodeIfNeeded:withTypeInferenceEnvironment: addMethod: {:(Std Reflection Semantic ASTAnalyzer)self :(Std Reflection AST Node)node :(Std Reflection Semantic TypeInferenceEnvironment)typeInferenceEnvironment :: Std Reflection AST Node |
        let: #oldTypeInferenceEnvironment with: (self typeInferenceEnvironment).
        self typeInferenceEnvironment: typeInferenceEnvironment.

        let: #analyzedNode with: (self analyzeNodeIfNeededWithCurrentExpectedType: node).
        self typeInferenceEnvironment: oldTypeInferenceEnvironment.
        analyzedNode
    }.
